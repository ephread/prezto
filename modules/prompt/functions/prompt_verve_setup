# Verve
#
# A theme based on the skwp and steeef themes
#  * Rbenv info shown on the right
#  * Git branch info on the left
#  * Single line prompt
#
# Authors:
#   Steve Losh <steve@stevelosh.com>
#   Bart Trojanowski <bart@jukie.net>
#   Brian Carper <brian@carper.ca>
#   steeef <steeef@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#   Yan Pritzker <yan@pritzker.ws>
#   Frédéric Maquin <frederic@maquin.eu>

function prompt_verve_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info on
    git-info
  fi

  # Get ruby information
  if (( $+functions[ruby-info] )); then
    ruby-info
  fi

  # Get node information
  if (( $+functions[node-info] )); then
    node-info
  fi
}

function prompt_verve_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  autoload -Uz add-zsh-hook

  add-zsh-hook precmd prompt_verve_precmd

  # Use extended color pallete if available.
  __PROMPT_verve_COLORS=(
    "%F{cyan}"
    "%F{yellow}"
    "%F{magenta}"
    "%F{red}"
    "%F{green}"
  )

  # git
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:branch' format "${__PROMPT_verve_COLORS[1]}%b%f"
  zstyle ':prezto:module:git:info:added' format "${__PROMPT_verve_COLORS[5]}+%f"
  zstyle ':prezto:module:git:info:deleted' format "${__PROMPT_verve_COLORS[4]}×%f"
  zstyle ':prezto:module:git:info:modified' format "${__PROMPT_verve_COLORS[2]}!%f"
  zstyle ':prezto:module:git:info:untracked' format "${__PROMPT_verve_COLORS[4]}?%f"
  zstyle ':prezto:module:git:info:ahead' format "${__PROMPT_verve_COLORS[5]}↑%f"
  zstyle ':prezto:module:git:info:behind' format "${__PROMPT_verve_COLORS[4]}↓%f"
  zstyle ':prezto:module:git:info:stashed' format "${__PROMPT_verve_COLORS[2]}≋%f"
  zstyle ':prezto:module:git:info:keys' format 'prompt' '(%A%B%b%d%a%m%u)%S '

  # ruby info (rvm, rbenv)
  zstyle ':prezto:module:ruby:info:version' format '[R:%v]'
  zstyle ':prezto:module:node:info:version' format '[N:%v]'

  PROMPT="%F{blue}%n %fat ${__PROMPT_verve_COLORS[2]}%m%f in ${__PROMPT_verve_COLORS[5]}%~%f "'$git_info[prompt]'"$ "
  RPROMPT='%F{magenta}${ruby_info[version]} %F{blue}${node_info[version]}'
}

prompt_verve_setup "$@"
